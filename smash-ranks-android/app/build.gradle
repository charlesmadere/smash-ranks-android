// Android
apply plugin: "com.android.application"

// Kotlin
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"

// Fabric / Firebase Crashlytics plugin
apply plugin: "io.fabric"

android {

    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 30001
        versionName "3.0.1"

        // needed for androidTest
        testInstrumentationRunner "com.garpr.android.test.AndroidTestRunner"
        testInstrumentationRunnerArguments clearPackageData: "true"

        // needed for Room migration tests in androidTest
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ "room.schemaLocation" : "$projectDir/schemas".toString() ]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    aaptOptions {
        cruncherEnabled false
    }

    sourceSets {
        androidTest {
            assets.srcDirs += files("$projectDir/schemas".toString())
            java.srcDirs += "src/baseTest/java"
        }

        test {
            java.srcDirs += "src/baseTest/java"
        }
    }

    testOptions {
        execution "ANDROIDX_TEST_ORCHESTRATOR"
    }

}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    // https://kotlinlang.org/docs/reference/using-gradle.html
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Kotlin Reflection (only for tests)
    // https://kotlinlang.org/docs/reference/using-gradle.html
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // AndroidX Activity
    // https://developer.android.com/jetpack/androidx/releases/activity
    implementation "androidx.activity:activity:$activity_version"

    // AndroidX AppCompat
    // https://developer.android.com/jetpack/androidx/releases/appcompat
    implementation "androidx.appcompat:appcompat:$appcompat_version"

    // AndroidX CardView
    // https://developer.android.com/jetpack/androidx/releases/cardview
    implementation "androidx.cardview:cardview:$cardview_version"

    // AndroidX ConstraintLayout
    // https://developer.android.com/jetpack/androidx/releases/constraintlayout
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"

    // AndroidX Core
    // https://developer.android.com/jetpack/androidx/releases/core
    implementation "androidx.core:core:$core_version"

    // AndroidX Fragment
    // https://developer.android.com/jetpack/androidx/releases/fragment
    implementation "androidx.fragment:fragment:$fragment_version"

    // AndroidX Palette
    // https://developer.android.com/jetpack/androidx/releases/palette
    implementation "androidx.palette:palette:$palette_version"

    // AndroidX RecyclerView
    // https://developer.android.com/jetpack/androidx/releases/recyclerview
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"

    // AndroidX Room
    // https://developer.android.com/jetpack/androidx/releases/room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

    // AndroidX SwipeRefreshLayout
    // https://developer.android.com/jetpack/androidx/releases/swiperefreshlayout
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swipe_refresh_layout_version"

    // AndroidX Test
    // https://developer.android.com/jetpack/androidx/releases/test
    androidTestImplementation "androidx.arch.core:core-testing:$architecture_test_version"
    androidTestImplementation "androidx.test:core:$testcore_version"
    androidTestImplementation "androidx.test:runner:$testcore_version"
    androidTestUtil "androidx.test:orchestrator:$testcore_version"

    // AndroidX ViewModel and LiveData
    // https://developer.android.com/jetpack/androidx/releases/lifecycle
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    // AndroidX ViewPager2
    // https://developer.android.com/jetpack/androidx/releases/viewpager2
    implementation "androidx.viewpager2:viewpager2:$viewpager_version"

    // AndroidX WorkManager
    // https://developer.android.com/jetpack/androidx/releases/work
    implementation "androidx.work:work-runtime:$workmanager_version"

    // Facebook's Fresco
    // https://github.com/facebook/fresco
    implementation "com.facebook.fresco:fresco:$fresco_version"
    implementation "com.facebook.fresco:imagepipeline-okhttp3:$fresco_version"

    // Google's Material Design Library
    // https://material.io/develop/android/
    implementation "com.google.android.material:material:$material_design_version"

    // JUnit 4
    // https://junit.org/junit4/
    androidTestImplementation "junit:junit:$junit_version"
    testImplementation "junit:junit:$junit_version"

    // Koin
    // https://github.com/InsertKoinIO/koin
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    testImplementation "org.koin:koin-test:$koin_version"

    // RxJava
    // https://github.com/ReactiveX/RxJava
    implementation "io.reactivex.rxjava2:rxjava:$rx_java_version"

    // Square's Moshi
    // https://github.com/square/moshi
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    // Square's OkHttp
    // https://github.com/square/okhttp
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"

    // Square's Retrofit
    // https://github.com/square/retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    // Fabric / Firebase Crashlytics
    // https://firebase.google.com/docs/crashlytics/get-started?platform=android
    implementation "com.crashlytics.sdk.android:crashlytics:$crashlytics_version"

}
