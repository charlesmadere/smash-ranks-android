// Fabric
buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

// Android
apply plugin: 'com.android.application'

// Kotlin
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

// Fabric
apply plugin: 'io.fabric'

// Fabric
repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 20000
        versionName "2.0.0"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    aaptOptions {
        cruncherEnabled false
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation "junit:junit:4.12"

    // Android ConstraintLayout
    // https://developer.android.com/training/constraint-layout/
    implementation "com.android.support.constraint:constraint-layout:$constraint_layout_version"

    // Android Support Library
    // https://developer.android.com/topic/libraries/support-library/index.html
    implementation "com.android.support:appcompat-v7:$appcompat_version"
    implementation "com.android.support:cardview-v7:$appcompat_version"
    implementation "com.android.support:design:$appcompat_version"
    implementation "com.android.support:palette-v7:$appcompat_version"
    implementation "com.android.support:recyclerview-v7:$appcompat_version"
    implementation "com.android.support:support-v4:$appcompat_version"

    // Facebook's Fresco
    // https://github.com/facebook/fresco
    implementation "com.facebook.fresco:fresco:$facebook_fresco_version"

    // Firebase JobDispatcher
    // https://github.com/firebase/firebase-jobdispatcher-android
    implementation "com.firebase:firebase-jobdispatcher:$firebase_jobdispatcher_version"

    // Google's Dagger
    // https://github.com/google/dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

    // Google's GSON
    // https://github.com/google/gson
    implementation "com.google.code.gson:gson:$gson_version"

    // Google Play Services
    // https://developers.google.com/android/guides/overview
    implementation "com.google.android.gms:play-services-base:$google_play_services_version"

    // Jake Wharton's KotterKnife
    // this is currently just a standalone file that was copy & pasted into our source code
    // https://github.com/JakeWharton/kotterknife

    // Kotlin
    // https://kotlinlang.org/docs/reference/using-gradle.html
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Robolectric
    // https://github.com/robolectric/robolectric
    testImplementation "org.robolectric:robolectric:$robolectric_version"

    // Square's Retrofit
    // https://github.com/square/retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // Fabric
    // https://fabric.io/kits/android/crashlytics/update
    implementation("com.crashlytics.sdk.android:crashlytics:2.9.4@aar") {
        transitive = true
    }

    // Annoying fix for compile error: "error cannot access nullable"
    implementation "com.google.code.findbugs:jsr305:3.0.2"
}
